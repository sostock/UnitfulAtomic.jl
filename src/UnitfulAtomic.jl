module UnitfulAtomic

import Unitful
using Unitful: @unit, Dimension, Dimensions, NoDims, NoUnits, Units, dimension, uconvert, ustrip

export auconvert, aunit, austrip

# The following five constants are used as the â€œbaseâ€ atomic units
"""
    UnitfulAtomic.me_au

A unit equal to the electron rest mass mâ‚‘ â‰ˆ 9.109,383,7015 Ã— 10^-31 kg.
Printed as "mâ‚‘".

`Unitful.me` is a quantity (with units `kg`) whereas `UnitfulAtomic.me_au` is a unit equal to
`Unitful.me`.

Dimension: [`Unitful.ðŒ`](@ref).

See also: `Unitful.me`, `Unitful.kg`.
"""
@unit me_au "mâ‚‘"  ElectronRestMass      Unitful.me                     false
"""
    UnitfulAtomic.e_au

A unit equal to the elementary charge e = 1.602,176,634 Ã— 10^-19 C.
Printed as "e".

`Unitful.q` is a quantity (with units `C`) whereas `UnitfulAtomic.e_au` is a unit equal to
`Unitful.q`.

Dimension: ðˆ ð“.

See also: `Unitful.q`, `Unitful.C`.
"""
@unit e_au  "e"   ElementaryCharge      Unitful.q                      false
"""
    UnitfulAtomic.Ä§_au

A unit equal to the reduced Planck constant Ä§ = h / 2Ï€ â‰ˆ 1.054,571,8176 Ã— 10^-34 J Ã— s.
Printed as "Ä§".

`Unitful.Ä§` is a quantity (with units `J Ã— s`) whereas `UnitfulAtomic.Ä§_au` is a unit equal to
`Unitful.Ä§`.

Dimension: ð‹^2 ðŒ ð“^-1.

See also: `Unitful.Ä§`, `Unitful.J`, `Unitful.s`.
"""
@unit Ä§_au  "Ä§"   ReducedPlanckConstant Unitful.Ä§                      false
"""
    UnitfulAtomic.k_au

A unit equal to the Boltzmann constant k = 1.380,649 Ã— 10^-23 J / K.
Printed as "k".

`Unitful.k` is a quantity (with units `J / K`) whereas `UnitfulAtomic.k_au` is a unit equal to
`Unitful.k`.

Dimension: ð‹^2 ðŒ ðš¯^-1 ð“^-2.

See also: `Unitful.k`, `Unitful.J`, `Unitful.K`.
"""
@unit k_au  "k"   BoltzmannConstant     Unitful.k                      false
"""
    UnitfulAtomic.a0_au
    UnitfulAtomic.bohr

A unit equal to the Bohr radius
```
aâ‚€ = 4Ï€ Ã— Îµ0 Ã— Ä§^2 / (me Ã— q^2)
   â‰ˆ 5.291,772,109,03 Ã— 10^-11 m.
```
Printed as "aâ‚€".

Dimension: ð‹.

See also: `Unitful.Îµ0`, `Unitful.Ä§`, `Unitful.me`, `Unitful.q`, `Unitful.m`.
"""
@unit a0_au "aâ‚€"  BohrRadius            5.291_772_109_03e-11*Unitful.m false # CODATA 2018 recommended value

# Hartree energy is derived from the base atomic units
"""
    UnitfulAtomic.Eh_au
    UnitfulAtomic.hartree

A unit equal to the Hartree energy
```
Eâ‚• = me Ã— q^4 / (4Ï€ Ã— Îµ0 Ã— Ä§)^2
   â‰ˆ 4.359,744,7222 Ã— 10^-18 J
   â‰ˆ 27.211,386,246 eV.
```
Printed as "Eâ‚•".

Dimension: ð‹^2 ðŒ ð“^-2.

See also: `Unitful.me`, `Unitful.q`, `Unitful.Îµ0`, `Unitful.Ä§`, `Unitful.J`, `Unitful.eV`, [`UnitfulAtomic.Ry`](@ref).
"""
@unit Eh_au "Eâ‚•"  HartreeEnergy         1Ä§_au^2/(me_au*a0_au^2)       false

# Units that are not Hartree atomic units, but are commonly used in atomic physics
"""
    UnitfulAtomic.Ry

A unit equal to the Rydberg energy
```
Ry = Eâ‚• / 2 
   = me Ã— q^4 / (8 Ã— Îµ0^2 Ã— h^2)
   â‰ˆ 2.179,872,3611 Ã— 10^-18 J
   â‰ˆ 13.605,693,1230 eV.
```

Dimension: ð‹^2 ðŒ ð“^-2.

See also: [`UnitfulAtomic.Eh_au`](@ref), `Unitful.me`, `Unitful.q`, `Unitful.Îµ0`, `Unitful.h`, `Unitful.J`, `Unitful.eV`.
"""
@unit Ry    "Ry"  RydbergEnergy         Eh_au//2                      false
"""
    Unitful.Î¼_N

A unit equal to the nuclear magneton
```
Î¼_N = q Ã— Ä§ / (2 Ã— mp)
    â‰ˆ 5.050,783,7461 Ã— 10^-27 J / T
    â‰ˆ 3.152,451,258,44 Ã— 10^-8 eV / T.
```

Dimension: ðˆ ð‹^2.

See also: `Unitful.q`, `Unitful.Ä§`, `Unitful.mp`, `Unitful.J`, `Unitful.T`, `Unitful.eV`.
"""
@unit Î¼_N   "Î¼_N" NuclearMagneton       e_au*Ä§_au/(2*Unitful.mp)      false

# Aliases for units
@doc @doc(a0_au) const bohr    = a0_au
@doc @doc(Eh_au) const hartree = Eh_au

"""
    aunit(x::Unitful.Quantity)
    aunit(x::Unitful.Units)
    aunit(x::Unitful.Dimensions)

Returns the appropriate atomic unit (a `Unitful.Units` object) for the dimension of `x`.

# Examples

```jldoctest
julia> aunit(2.3u"cm")
aâ‚€

julia> aunit(u"T")
aâ‚€^-2 e^-1 Ä§
```
"""
aunit(x) = aunit(dimension(x))

# `aunit` for `Dimension` types
aunit(x::Dimension{:Length})      = (a0_au)^x.power
aunit(x::Dimension{:Mass})        = (me_au)^x.power
aunit(x::Dimension{:Time})        = (Ä§_au/Eh_au)^x.power
aunit(x::Dimension{:Current})     = (e_au*Eh_au/Ä§_au)^x.power
aunit(x::Dimension{:Temperature}) = (Eh_au/k_au)^x.power

# For dimensions not specified above, there is no atomic unit.
aunit(::Dimension{D}) where D = throw(ArgumentError("no atomic unit defined for dimension $D."))

# `aunit` for `Dimensions` types
@generated aunit(::Dimensions{N}) where N = prod(aunit, N)
aunit(::typeof(NoDims)) = NoUnits

# Simplifications for some derived dimensions, so that e.g. `aunit(u"J")` returns `Eâ‚•`
# instead of `aâ‚€^2 mâ‚‘ Eâ‚•^2 Ä§^-2`. The following units/dimensions are considered:
#   * Energy: Eâ‚•
#   * Momentum: Ä§/aâ‚€
#   * Action/angular momentum: Ä§
#   * Force: Eâ‚•/aâ‚€
#   * Pressure: Eâ‚•/aâ‚€^3
#   * E-field: Eâ‚•/(e*aâ‚€)
#   * B-field: Ä§/(e*aâ‚€^2)
#   * Voltage/electric potential: Eâ‚•/e
#   * Magnetic dipole moment: e*Ä§/mâ‚‘
#   * Entropy: k
#   * 1st hyperpolarizability: e^3*aâ‚€^3/Eâ‚•^2
#   * 2nd hyperpolarizability: e^4*aâ‚€^4/Eâ‚•^3
#   * E-field gradient: Eâ‚•/(e*aâ‚€^2)
#   * Electric polarizability: e^2*aâ‚€^2/Eâ‚•
#   * Permittivity: e^2/(aâ‚€*Eâ‚•)
for unit in (:(Eh_au), :(Ä§_au/a0_au), :(Ä§_au), :(Eh_au/a0_au), :(Eh_au/a0_au^3),
             :(Eh_au/(e_au*a0_au)), :(Ä§_au/(e_au*a0_au^2)), :(Eh_au/e_au), :(e_au*Ä§_au/me_au),
             :(k_au), :(e_au^3*a0_au^3/Eh_au^2), :(e_au^4*a0_au^4/Eh_au^3),
             :(Eh_au/(e_au*a0_au^2)), :(e_au^2*a0_au^2/Eh_au), :(e_au^2/(a0_au*Eh_au)))
    @eval aunit(::typeof(dimension($unit))) = $unit
end

"""
    auconvert(x::Unitful.Quantity)

Convert a quantity to the appropriate atomic unit.

# Examples

```jldoctest
julia> auconvert(13.6u"eV")
0.4997907858599377 Eâ‚•

julia> auconvert(20u"nm")
377.94522509156565 aâ‚€
```
"""
auconvert(x) = uconvert(aunit(x), x)

"""
    auconvert(u::Unitful.Units, x::Number)

Interpret `x` as a quantity given in atomic units and convert it to the unit `u`.

# Examples

```jldoctest
julia> auconvert(u"eV", 1)  # convert 1 Eâ‚• to eV
27.211386013449417 eV

julia> auconvert(u"m", 1)   # convert 1 aâ‚€ to m
5.2917721067e-11 m
```
"""
auconvert(u::Units, x::Number) = uconvert(u, x*aunit(u))

"""
    austrip(x::Unitful.Quantity)

Returns the value of the quantity converted to atomic units as a number type (i.e., with the
units removed). This is equivalent to `Unitful.ustrip(auconvert(x))`.

# Examples

```jldoctest
julia> austrip(13.6u"eV")
0.4997907858599377

julia> austrip(20u"nm")
377.94522509156565
```
"""
austrip(x) = ustrip(auconvert(x))

# In order to enable precompilation, some things need to be set at runtime
__init__() = Unitful.register(UnitfulAtomic)

end # module UnitfulAtomic
